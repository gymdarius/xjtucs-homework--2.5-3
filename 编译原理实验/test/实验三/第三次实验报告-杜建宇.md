# 编译器专题实验报告

## 实验三:语法分析

### 实验内容（必做）：

####  任务一、完成计算器

+ 支持加减乘除和括号

#### 任务二、完成文法分析，实现输出类似语法分析树的结果

+ 需要考虑出错处理

### 实验内容（选做）：

####  任务三、在任务二的基础上使用lex文件完成词法分析识别词素，实现flex和bison的联合编译，使用脚本文件或make或批处理文件实现一键编译。

### 实验结果：（独立模式）：

**任务一和任务二已经使用脚本文件实现一键编译**

####  任务一结果

![image-20240514151434218](https://gitee.com/du-jianyu1012/img/raw/master/picture/image-20240514151434218.png)

#### 任务二结果

输入设置为1.c

```c
//1.c
int main(){
	i=1;
}
```

批处理结果为：

![image-20240514151638147](https://gitee.com/du-jianyu1012/img/raw/master/picture/image-20240514151638147.png)

![image-20240514151627468](https://gitee.com/du-jianyu1012/img/raw/master/picture/image-20240514151627468.png)

输入设置为2.c

```c
//2.c
int main(){
	hello
} 
```

批处理结果为

![image-20240514151659643](https://gitee.com/du-jianyu1012/img/raw/master/picture/image-20240514151659643.png)

由上面实验结果可以看出，成功实现了输出类似语法分析树的结果，同时考虑了出错处理

### 另外遇到的问题和解决思路（可选）：

 在实现Bison文件时，定义翻译规则时考虑了c程序的识别，该部分耗时较长。

### 代码很原创（可选）：

#### 任务一代码：

**.bat文件**

```bash
@echo off
set str=%1
@echo on
bison --yacc -dv %str%.y
flex %str%.l
gcc -o %str% y.tab.c lex.yy.c
test.exe
```

**test.l**

```c
%{
    #include<stdlib.h>
    void yyerror(char*);
    #include "calc.tab.h"  
%}

%%
[0-9]+      {yylval=atoi(yytext); return NUMBER;}
[-+*/()\n]   {return *yytext;}
[ \t]       ;

.    	    yyerror("Error");

%%
int yywrap(void)
{
  return 1;
}
```

**test.y**

```c
%token NUMBER
%left '+' '-' '*' '/'

%{
    #include <stdio.h>
    #include <math.h>
    void yyerror(char *s);
    int yylex();    
%}

%%
    
    prog: 
        | prog expr '\n' { printf("%d\n", $2); };
    expr:
        expr '+' term { $$ = $1 + $3; }
        | expr '-' term { $$ = $1 - $3; }
        | term
    term:
        | term '*' factor { $$ = $1 * $3; }
        | term '/' factor { $$ = $1 / $3; }        
        | factor
        ;
    factor:
        NUMBER {};
        | '(' expr ')' { $$ = $2; }
        ;
%%

void yyerror(char *s) {
    fprintf(stderr, "%s\n", s);
}

int main() {
    yyparse();
    return 0;
}
```

#### 任务二代码：

**.bat文件**

```bash
@echo off
set str=%1
@echo on
flex %str%.l
bison --yacc -dv %str%.y
gcc -o %str% y.tab.c lex.yy.c
test.exe <1.c
 
```

**test.l文件**

```c
%option yylineno
 
%{
    #include "y.tab.h"
    #define UNEXPECTED 0
%}
 
INT                 int
VOID                void
CONST               const
IF                  if
ELSE                else
WHILE               while
BREAK               break
CONTINUE            continue
RETURN              return
MULDIVSUR           "*"|"/"|"%"
ADDSUB              "+"|"-"
CMP                 "<"|">"|"<="|">="
EQNEQ               "=="|"!="
ASSIGN              "="
NONZERO             [1-9]
DIGIT               [0-9]
LETTER              [A-Za-z]
OCTAL_DIGIT         [0-7]
OCTAL_CONST         0{OCTAL_DIGIT}*
ILLEGAL_OCTAL_CONST 0[0-9a-wy-zA-WY-Z]({LETTER}|{DIGIT})*
HEX_PREFIX          0x|0X
HEX_DIGIT           [0-9a-fA-F]
HEX_CONST           {HEX_PREFIX}{HEX_DIGIT}+
ILLEGAL_HEX_CONST   {HEX_PREFIX}({LETTER}|{DIGIT})*
NONDIGIT            {LETTER}|"_"
ID                  {NONDIGIT}({DIGIT}|{NONDIGIT})*
DEC_CONST           {NONZERO}{DIGIT}*  
COMMENT1            "/*"[^*]*"*"+([^*/][^*]*"*"+)*"/"
COMMENT2            "//".*                                                                                
 
%%
 
{INT}                   { yylval.str=strdup(yytext); return INT; }
{VOID}                  { yylval.str=strdup(yytext); return VOID; }
{OCTAL_CONST}           { yylval.str=strdup(yytext); return OCTAL_CONST; }
{ILLEGAL_OCTAL_CONST}   { yylval.str=strdup(yytext); return HEX_CONST; }
{HEX_CONST}             { yylval.str=strdup(yytext); return HEX_CONST; }
{ILLEGAL_HEX_CONST}     { yylval.str=strdup(yytext); return DEC_CONST; }
{DEC_CONST}             { yylval.str=strdup(yytext); return DEC_CONST; }
{CONST}                 { yylval.str=strdup(yytext); return CONST; }
{IF}                    { yylval.str=strdup(yytext); return IF; }
{ELSE}                  { yylval.str=strdup(yytext); return ELSE; }
{WHILE}                 { yylval.str=strdup(yytext); return WHILE; }
{BREAK}                 { yylval.str=strdup(yytext); return BREAK; }
{CONTINUE}              { yylval.str=strdup(yytext); return CONTINUE; }
{RETURN}                { yylval.str=strdup(yytext); return RETURN; }
{MULDIVSUR}             { yylval.str=strdup(yytext); return MULDIVSUR; } 
{ADDSUB}                { yylval.str=strdup(yytext); return ADDSUB; } 
{CMP}                   { yylval.str=strdup(yytext); return CMP; } 
{EQNEQ}                 { yylval.str=strdup(yytext); return EQNEQ; } 
{ASSIGN}                { yylval.str=strdup(yytext); return ASSIGN; }
{ID}                    { yylval.str=strdup(yytext); return ID; }
"("                     { yylval.str=strdup(yytext); return yytext[0]; }
")"                     { yylval.str=strdup(yytext); return yytext[0]; }
"["                     { yylval.str=strdup(yytext); return yytext[0]; }
"]"                     { yylval.str=strdup(yytext); return yytext[0]; }
"{"                     { yylval.str=strdup(yytext); return yytext[0]; }
"}"                     { yylval.str=strdup(yytext); return yytext[0]; }
";"                     { yylval.str=strdup(yytext); return yytext[0]; }
","                     { yylval.str=strdup(yytext); return yytext[0]; }
"&&"                    { yylval.str=strdup(yytext); return AND; }
"||"                    { yylval.str=strdup(yytext); return OR; }
{COMMENT1}|{COMMENT2}   { }
[ \t\n]                 { }
.                       { yylval.str=strdup(yytext); return UNEXPECTED; }
%%
 
int yywrap(void) 
{ 
    return 1;
}
```

**test.y文件**

```c
%start CompUnit
%expect 1
 
%{
    #include "parser.h"
%}
 
%union
{
    int     num;
    char*   str;
    struct ASTnode* node; /*"struct" is indispensable*/
}
 
%token <str> INT VOID CONST IF ELSE WHILE BREAK CONTINUE RETURN ID OCTAL_CONST HEX_CONST DEC_CONST
%right <str> ASSIGN
%left <str> OR
%left <str> AND
%left <str> EQNEQ
%left <str> CMP
%left <str> ADDSUB
%left <str> MULDIVSUR
 
%type<node> Number CompUnit Decl FuncDef ConstDecl VarDecl ConstDef ConstDefBlock ConstExpBlock ConstInitVal ConstExp ConstInitFlag ConstValBlock VarDef
    VarDefFlag InitVal Exp InitValFlag InitValBlock FuncFParams Block FuncFParam FuncFParamBlock ExpBlockFlag ExpBlock BlockItemBlock BlockItem 
    Stmt LVal ExpFlag StmtFlag Cond AddExp LOrExp PrimaryExp UnaryExp FuncFParamsFlag FuncRParams UNARYOP CommaExpBlock MulExp RelExp EqExp LAndExp
 
%%
CompUnit:       CompUnit Decl                           {
                                                            connectASTnode(2,$1,$2);
                                                            ASThead=$$=newASTnode(TEXT,"CompUnit",0,NULL,$1);
                                                        }
                | CompUnit FuncDef                      {
                                                            connectASTnode(2,$1,$2);
                                                            ASThead=$$=newASTnode(TEXT,"CompUnit",0,NULL,$1);
                                                        }
                | Decl                                  {ASThead=$$=newASTnode(TEXT,"CompUnit",0,NULL,$1);}
                | FuncDef                               {ASThead=$$=newASTnode(TEXT,"CompUnit",0,NULL,$1);}
Decl:           ConstDecl                               {$$=newASTnode(TEXT,"Decl",0,NULL,$1);}
                | VarDecl                               {$$=newASTnode(TEXT,"Decl",0,NULL,$1);}
ConstDecl:      CONST INT ConstDef ConstDefBlock ';'    {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL),
                                                                    *n2=newASTnode(TEXT,$2,0,NULL,NULL),
                                                                    *n5=newASTnode(TEXT,";",0,NULL,NULL);
                                                            connectASTnode(5,n1,n2,$3,$4,n5);
                                                            $$=newASTnode(TEXT,"ConstDecl",0,NULL,n1);
                                                        }
ConstDefBlock:  ConstDefBlock ',' ConstDef              {
                                                            ASTnode *n=newASTnode(TEXT,",",0,$3,NULL);
                                                            connectASTnode(3,$1,n,$3);
                                                            $$=newASTnode(TEXT,"ConstDefBlock",0,NULL,$1);
                                                        }
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"ConstDefBlock",0,NULL,n);
                                                        }
ConstDef:       ID ConstExpBlock ASSIGN ConstInitVal    {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL),
                                                                    *n3=newASTnode(TEXT,$3,0,NULL,NULL);
                                                            connectASTnode(4,n1,$2,n3,$4);
                                                            $$=newASTnode(TEXT,"ConstDef",0,NULL,n1);
                                                        }
ConstExpBlock:  ConstExpBlock '[' ConstExp ']'          {
                                                            ASTnode *n2=newASTnode(TEXT,"[",0,NULL,NULL),
                                                                    *n4=newASTnode(TEXT,"]",0,NULL,NULL);
                                                            connectASTnode(4,$1,n2,$3,n4);
                                                            $$=newASTnode(TEXT,"ConstExpBlock",0,NULL,$1);
                                                        }
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"ConstExpBlock",0,NULL,n);
                                                        }
ConstInitVal:   ConstExp                                {$$=newASTnode(TEXT,"ConstInitVal",0,NULL,$1);}
                |'{'ConstInitFlag'}'                    {
                                                            ASTnode *n1=newASTnode(TEXT,"{",0,NULL,NULL),
                                                                    *n3=newASTnode(TEXT,"}",0,NULL,NULL);
                                                            connectASTnode(3,n1,$2,n3);
                                                            $$=newASTnode(TEXT,"ConstInitVal",0,NULL,n1);
                                                        }
ConstInitFlag:  ConstInitVal ConstValBlock              {
                                                            connectASTnode(2,$1,$2);
                                                            $$=newASTnode(TEXT,"ConstInitFlag",0,NULL,$1);
                                                        }
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"ConstInitFlag",0,NULL,n);
                                                        }
ConstValBlock:  ConstValBlock ',' ConstInitVal          {
                                                            ASTnode *n2=newASTnode(TEXT,",",0,NULL,NULL);
                                                            connectASTnode(3,$1,n2,$3);
                                                            $$=newASTnode(TEXT,"ConstValBlock",0,NULL,$1);
                                                        }
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"ConstValBlock",0,NULL,n);
                                                        }
VarDecl:        INT VarDef VarDefFlag ';'               {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL),
                                                                    *n4=newASTnode(TEXT,";",0,NULL,NULL);
                                                            connectASTnode(4,n1,$2,$3,n4);
                                                            $$=newASTnode(TEXT,"VarDecl",0,NULL,n1);
                                                        }
VarDefFlag:    ',' VarDef VarDefFlag                    {
                                                            ASTnode *n1=newASTnode(TEXT,",",0,NULL,NULL);
                                                            connectASTnode(3,n1,$2,$3);
                                                            $$=newASTnode(TEXT,"VarDefFlag",0,NULL,n1);
                                                        }
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"VarDefFlag",0,NULL,n);
                                                        }
VarDef:         ID ConstExpBlock                        {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL);
                                                            connectASTnode(2,n1,$2);
                                                            $$=newASTnode(TEXT,"VarDef",0,NULL,n1);
                                                        }
                | ID ConstExpBlock ASSIGN InitVal       {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL),
                                                                    *n3=newASTnode(TEXT,$3,0,NULL,NULL);
                                                            connectASTnode(4,n1,$2,n3,$4);
                                                            $$=newASTnode(TEXT,"VarDef",0,NULL,n1);
                                                        }
InitVal:          Exp                                   {$$=newASTnode(TEXT,"InitVal",0,NULL,$1);}
                | '{'InitValFlag'}'                     {
                                                            ASTnode *n1=newASTnode(TEXT,"{",0,NULL,NULL),
                                                                    *n3=newASTnode(TEXT,"}",0,NULL,NULL);
                                                            connectASTnode(3,n1,$2,n3);
                                                            $$=newASTnode(TEXT,"InitVal",0,NULL,n1);
                                                        }
InitValFlag:    InitVal InitValBlock                    {
                                                            connectASTnode(2,$1,$2);
                                                            $$=newASTnode(TEXT,"InitValFlag",0,NULL,$1);
                                                        }
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"InitValFlag",0,NULL,n);
                                                        }
InitValBlock:   InitValBlock ',' InitVal                {
                                                            ASTnode *n2=newASTnode(TEXT,",",0,NULL,NULL);
                                                            connectASTnode(3,$1,n2,$3);
                                                            $$=newASTnode(TEXT,"InitValBlock",0,NULL,$1);
                                                        }
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"InitValFlag",0,NULL,n);
                                                        }
FuncDef:        INT ID '(' FuncFParams')' Block         {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL),
                                                                    *n2=newASTnode(TEXT,$2,0,NULL,NULL),
                                                                    *n3=newASTnode(TEXT,"(",0,NULL,NULL),
                                                                    *n5=newASTnode(TEXT,")",0,NULL,NULL);
                                                            connectASTnode(6,n1,n2,n3,$4,n5,$6);
                                                            $$=newASTnode(TEXT,"FuncDef",0,NULL,n1);
                                                        }
                | VOID ID '(' FuncFParams')' Block      {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL),
                                                                    *n2=newASTnode(TEXT,$2,0,NULL,NULL),
                                                                    *n3=newASTnode(TEXT,"(",0,NULL,NULL),
                                                                    *n5=newASTnode(TEXT,")",0,NULL,NULL);
                                                            connectASTnode(6,n1,n2,n3,$4,n5,$6);
                                                            $$=newASTnode(TEXT,"FuncDef",0,NULL,n1);
                                                        }
FuncFParams:    FuncFParam FuncFParamBlock              {
                                                            connectASTnode(2,$1,$2);
                                                            $$=newASTnode(TEXT,"FuncFParams",0,NULL,$1);
                                                        }
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"FuncFParams",0,NULL,n);
                                                        }
FuncFParamBlock:FuncFParamBlock ',' FuncFParam          {
                                                            ASTnode *n2=newASTnode(TEXT,",",0,NULL,NULL);
                                                            connectASTnode(3,$1,n2,$3);
                                                            $$=newASTnode(TEXT,"FuncFParamBlock",0,NULL,$1);
                                                        }
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"FuncFParamBlock",0,NULL,n);
                                                        }
FuncFParam:     INT ID ExpBlockFlag                     {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL),
                                                                    *n2=newASTnode(TEXT,$2,0,NULL,NULL);
                                                            connectASTnode(3,n1,n2,$3);
                                                            $$=newASTnode(TEXT,"FuncFParam",0,NULL,n1);
                                                        }
ExpBlockFlag:   '['']' ExpBlock                         {
                                                            ASTnode *n1=newASTnode(TEXT,"[",0,NULL,NULL),
                                                                    *n2=newASTnode(TEXT,"]",0,NULL,NULL);
                                                            connectASTnode(3,n1,n2,$3);
                                                            $$=newASTnode(TEXT,"ExpBlockFlag",0,NULL,n1);
                                                        }
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"ExpBlockFlag",0,NULL,n);
                                                        }
ExpBlock:       ExpBlock '['Exp']'                      {
                                                            ASTnode *n2=newASTnode(TEXT,"[",0,NULL,NULL),
                                                                    *n4=newASTnode(TEXT,"]",0,NULL,NULL);
                                                            connectASTnode(4,$1,n2,$3,n4);
                                                            $$=newASTnode(TEXT,"FuncDef",0,NULL,$1);
                                                        }
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"ExpBlock",0,NULL,n);
                                                        }
Block:          '{' BlockItemBlock '}'                  {
                                                            ASTnode *n1=newASTnode(TEXT,"{",0,NULL,NULL),
                                                                    *n3=newASTnode(TEXT,"}",0,NULL,NULL);
                                                            connectASTnode(3,n1,$2,n3);
                                                            $$=newASTnode(TEXT,"Block",0,NULL,n1);
                                                        }
BlockItemBlock: BlockItemBlock BlockItem                {
                                                            connectASTnode(2,$1,$2);
                                                            $$=newASTnode(TEXT,"BlockItemBlock",0,NULL,$1);
                                                        }
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"BlockItemBlock",0,NULL,n);
                                                        }
BlockItem:        Decl                                  {$$=newASTnode(TEXT,"BlockItem",0,NULL,$1);}
                | Stmt                                  {$$=newASTnode(TEXT,"BlockItem",0,NULL,$1);}
Stmt:           LVal ASSIGN Exp ';' %prec ASSIGN        {
                                                            ASTnode *n2=newASTnode(TEXT,$2,0,NULL,NULL),
                                                                    *n4=newASTnode(TEXT,";",0,NULL,NULL);
                                                            connectASTnode(4,$1,n2,$3,n4);
                                                            $$=newASTnode(TEXT,"Stmt",0,NULL,$1);
                                                        }
                | ExpFlag';'                            {
                                                            ASTnode *n2=newASTnode(TEXT,";",0,NULL,NULL);
                                                            connectASTnode(2,$1,n2);
                                                            $$=newASTnode(TEXT,"Stmt",0,NULL,$1);
                                                        }
                | Block                                 {$$=newASTnode(TEXT,"Stmt",0,NULL,$1);}
                | IF'('Cond')' Stmt StmtFlag            {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL),
                                                                    *n2=newASTnode(TEXT,"(",0,NULL,NULL),
                                                                    *n4=newASTnode(TEXT,")",0,NULL,NULL);
                                                            connectASTnode(6,n1,n2,$3,n4,$5,$6);
                                                            $$=newASTnode(TEXT,"Stmt",0,NULL,n1);
                                                        }
                | WHILE'('Cond')' Stmt                  {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL),
                                                                    *n2=newASTnode(TEXT,"(",0,NULL,NULL),
                                                                    *n4=newASTnode(TEXT,")",0,NULL,NULL);
                                                            connectASTnode(5,n1,n2,$3,n4,$5);
                                                            $$=newASTnode(TEXT,"Stmt",0,NULL,n1);
                                                        }
                | BREAK';'                              {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL),
                                                                    *n2=newASTnode(TEXT,";",0,NULL,NULL);
                                                            connectASTnode(2,n1,n2);
                                                            $$=newASTnode(TEXT,"Stmt",0,NULL,n1);
                                                        }
                | CONTINUE';'                           {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL),
                                                                    *n2=newASTnode(TEXT,";",0,NULL,NULL);
                                                            connectASTnode(2,n1,n2);
                                                            $$=newASTnode(TEXT,"Stmt",0,NULL,n1);
                                                        }
                | RETURN ExpFlag';'                     {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL),
                                                                    *n3=newASTnode(TEXT,";",0,NULL,NULL);
                                                            connectASTnode(3,n1,$2,n3);
                                                            $$=newASTnode(TEXT,"Stmt",0,NULL,n1);
                                                        }
ExpFlag:          Exp                                   {$$=newASTnode(TEXT,"ExpFlag",0,NULL,$1);}
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"ExpFlag",0,NULL,n);
                                                        }
StmtFlag:       ELSE Stmt                               {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL);
                                                            connectASTnode(2,n1,$2);
                                                            $$=newASTnode(TEXT,"StmtFlag",0,NULL,n1);
                                                        }
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"StmtFlag",0,NULL,n);
                                                        }
Exp:            AddExp                                  {$$=newASTnode(TEXT,"Exp",0,NULL,$1);}
Cond:           LOrExp                                  {$$=newASTnode(TEXT,"Cond",0,NULL,$1);}
LVal:           ID ExpBlock                             {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL);
                                                            connectASTnode(2,n1,$2);
                                                            $$=newASTnode(TEXT,"LVal",0,NULL,n1);
                                                        }
PrimaryExp:     '('Exp')'                               {
                                                            ASTnode *n1=newASTnode(TEXT,"(",0,NULL,NULL),
                                                                    *n3=newASTnode(TEXT,")",0,NULL,NULL);
                                                            connectASTnode(3,n1,$2,n3);
                                                            $$=newASTnode(TEXT,"PrimaryExp",0,NULL,n1);
                                                        }
                | LVal                                  {$$=newASTnode(TEXT,"PrimaryExp",0,NULL,$1);}
                | Number                                {$$=newASTnode(TEXT,"PrimaryExp",0,NULL,$1);}
Number:         OCTAL_CONST                             {
                                                            ASTnode *n=newASTnode(NUM,NULL,OCT2DEC($1),NULL,NULL);
                                                            $$=newASTnode(TEXT,"Number",0,NULL,n);
                                                        }
                | HEX_CONST                             {
                                                            ASTnode *n=newASTnode(NUM,NULL,HEX2DEC($1),NULL,NULL);
                                                            $$=newASTnode(TEXT,"Number",0,NULL,n);
                                                        }
                | DEC_CONST                             {
                                                            ASTnode *n=newASTnode(NUM,NULL,atoi($1),NULL,NULL);
                                                            $$=newASTnode(TEXT,"Number",0,NULL,n);
                                                        }
UnaryExp:       PrimaryExp                              {$$=newASTnode(TEXT,"UnaryExp",0,NULL,$1);}
                | ID '(' FuncFParamsFlag ')'            {
                                                            ASTnode *n1=newASTnode(TEXT,$1,0,NULL,NULL),
                                                                    *n2=newASTnode(TEXT,"(",0,NULL,NULL),
                                                                    *n4=newASTnode(TEXT,")",0,NULL,NULL);
                                                            connectASTnode(4,n1,n2,$3,n4);
                                                            $$=newASTnode(TEXT,"PrimaryExp",0,NULL,n1);
                                                        }
                | UNARYOP UnaryExp                      {
                                                            connectASTnode(2,$1,$2);
                                                            $$=newASTnode(TEXT,"UnaryExp",0,NULL,$1);
                                                        }
FuncFParamsFlag:FuncRParams                             {$$=newASTnode(TEXT,"FuncFParamsFlag",0,NULL,$1);}
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"FuncFParamsFlag",0,NULL,n);
                                                        }
FuncRParams:    Exp CommaExpBlock                       {
                                                            connectASTnode(2,$1,$2);
                                                            $$=newASTnode(TEXT,"FuncRParams",0,NULL,$1);
                                                        }
CommaExpBlock:  CommaExpBlock ',' Exp                   {
                                                            ASTnode *n2=newASTnode(TEXT,",",0,NULL,NULL);
                                                            connectASTnode(3,$1,n2,$3);
                                                            $$=newASTnode(TEXT,"CommaExpBlock",0,NULL,$1);
                                                        }
                | /*E*/                                 {
                                                            ASTnode *n=newASTnode(TEXT,"E",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"CommaExpBlock",0,NULL,n);
                                                        }
UNARYOP:        ADDSUB                                  {
                                                            ASTnode *n=newASTnode(TEXT,$1,0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"UNARYOP",0,NULL,n);
                                                        }
                | '!'                                   {
                                                            ASTnode *n=newASTnode(TEXT,"!",0,NULL,NULL);
                                                            $$=newASTnode(TEXT,"UNARYOP",0,NULL,n);
                                                        }
MulExp:         UnaryExp                                {$$=newASTnode(TEXT,"MulExp",0,NULL,$1);}
                | MulExp MULDIVSUR UnaryExp             {
                                                            ASTnode *n2=newASTnode(TEXT,$2,0,NULL,NULL);
                                                            connectASTnode(3,$1,n2,$3);
                                                            $$=newASTnode(TEXT,"MulExp",0,NULL,$1);
                                                        }
AddExp:         MulExp                                  {$$=newASTnode(TEXT,"AddExp",0,NULL,$1);}
                | AddExp ADDSUB MulExp                  {
                                                            ASTnode *n2=newASTnode(TEXT,$2,0,NULL,NULL);
                                                            connectASTnode(3,$1,n2,$3);
                                                            $$=newASTnode(TEXT,"MulExp",0,NULL,$1);
                                                        }
RelExp:         AddExp                                  {$$=newASTnode(TEXT,"RelExp",0,NULL,$1);}
                | RelExp CMP AddExp                     {
                                                            ASTnode *n2=newASTnode(TEXT,$2,0,NULL,NULL);
                                                            connectASTnode(3,$1,n2,$3);
                                                            $$=newASTnode(TEXT,"RelExp",0,NULL,$1);
                                                        }
EqExp:          RelExp                                  {$$=newASTnode(TEXT,"EqExp",0,NULL,$1);}
                | EqExp EQNEQ RelExp                    {
                                                            ASTnode *n2=newASTnode(TEXT,$2,0,NULL,NULL);
                                                            connectASTnode(3,$1,n2,$3);
                                                            $$=newASTnode(TEXT,"EqExp",0,NULL,$1);
                                                        }
LAndExp:        EqExp                                   {$$=newASTnode(TEXT,"LAndExp",0,NULL,$1);}
                | LAndExp AND EqExp                     {
                                                            ASTnode *n2=newASTnode(TEXT,$2,0,NULL,NULL);
                                                            connectASTnode(3,$1,n2,$3);
                                                            $$=newASTnode(TEXT,"LAndExp",0,NULL,$1);
                                                        }
LOrExp:         LAndExp                                 {$$=newASTnode(TEXT,"LOrExp",0,NULL,$1);}
                | LOrExp OR LAndExp                     {
                                                            ASTnode *n2=newASTnode(TEXT,$2,0,NULL,NULL);
                                                            connectASTnode(3,$1,n2,$3);
                                                            $$=newASTnode(TEXT,"LOrExp",0,NULL,$1);
                                                        }
ConstExp:       AddExp                                  {$$=newASTnode(TEXT,"ConstExp",0,NULL,$1);}
%%
 
int main()
{
    yyparse();
    outputAST(ASThead,0);
    freeAST(ASThead);
    return 0;
}
```

